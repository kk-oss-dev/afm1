apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    ndkVersion "16.1.4479499"
    defaultConfig {
        applicationId "com.github.axet.filemanager"
        minSdkVersion 9
        targetSdkVersion 29
        versionCode 179
        versionName "0.5.0"
        ndk {
            abiFilters "armeabi", "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
        }
    }
    signingConfigs {
        release {
            storeFile file(project.findProperty('RELEASE_STORE_FILE') ?: 'none')
            storePassword project.findProperty('RELEASE_STORE_PASSWORD')
            keyAlias project.findProperty('RELEASE_KEY_ALIAS')
            keyPassword project.findProperty('RELEASE_KEY_PASSWORD')
        }
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFileName = org.apache.commons.io.FilenameUtils.getExtension(android.defaultConfig.applicationId) + "-" + android.defaultConfig.versionName + "-" + variant.buildType.name + ".apk"
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
}

dependencies {
    implementation 'com.github.axet:android-library:1.34.4' // implementation project(':android-library')
    implementation 'com.android.support:design:25.3.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation('com.github.axet:wget:1.7.0') { exclude group: 'org.json', module: 'json' }
    testImplementation 'junit:junit:4.12'
}
